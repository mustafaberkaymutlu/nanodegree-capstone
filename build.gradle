buildscript {
    def versionMajor = 1
    def versionMinor = 0
    def versionPatch = 0
    def versionBuild = 0 // bump for dogfood builds, public betas, etc.

    ext.versions = [
            'compileSdk': 27,
            'minSdk': 21,
            'targetSdk': 27,

            'versionCode' : versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild,
            'versionName' : "${versionMajor}.${versionMinor}.${versionPatch}",

            'supportLibrary': '27.1.1',
            'constraintLayout': '1.1.2',
            'dagger': '2.16',
            'retrofit': '2.4.0',
            'glide': '4.7.1',
            'archComponents': '1.1.1',
            'firebase': '16.0.1'
    ]

    ext.deps = [
            'support': [
                    'v4': "com.android.support:support-v4:${versions.supportLibrary}",
                    'v13': "com.android.support:support-v13:${versions.supportLibrary}",
                    'appCompat': "com.android.support:appcompat-v7:${versions.supportLibrary}",
                    'annotations': "com.android.support:support-annotations:${versions.supportLibrary}",
                    'design': "com.android.support:design:${versions.supportLibrary}",
                    'recyclerView': "com.android.support:recyclerview-v7:${versions.supportLibrary}",
                    'cardView': "com.android.support:cardview-v7:${versions.supportLibrary}",
                    'constraintLayout': "com.android.support.constraint:constraint-layout:${versions.constraintLayout}",
                    'customTabs': "com.android.support:customtabs:${versions.supportLibrary}",
            ],
            'dagger': [
                    'runtime': "com.google.dagger:dagger:${versions.dagger}",
                    'compiler': "com.google.dagger:dagger-compiler:${versions.dagger}",
            ],
            'daggerAndroid': [
                    'runtime': "com.google.dagger:dagger-android-support:${versions.dagger}",
                    'compiler':"com.google.dagger:dagger-android-processor:${versions.dagger}"
            ],
            'retrofit': [
                    'base': "com.squareup.retrofit2:retrofit:${versions.retrofit}",
                    'gson': "com.squareup.retrofit2:converter-gson:${versions.retrofit}",
                    'rxAdapter': "com.squareup.retrofit2:adapter-rxjava2:${versions.retrofit}"
            ],
            'glide': [
                    "runtime": "com.github.bumptech.glide:glide:${versions.glide}",
                    "compiler": "com.github.bumptech.glide:compiler:${versions.glide}"
            ],
            'fabric': [
                    'plugin': 'io.fabric.tools:gradle:1.25.4',
                    'crashlytics': 'com.crashlytics.sdk.android:crashlytics:2.9.3@aar'
            ],
            'archComponents': [
                    'room': [
                            'runtime': "android.arch.persistence.room:runtime:${versions.archComponents}",
                            'compiler': "android.arch.persistence.room:compiler:${versions.archComponents}",
                            'rxJava': "android.arch.persistence.room:rxjava2:${versions.archComponents}"
                    ],
                    'lifecycle': [
                            'extensions': "android.arch.lifecycle:extensions:${versions.archComponents}",
                            'common': "android.arch.lifecycle:common-java8:${versions.archComponents}",
                            'reactiveStreams': "android.arch.lifecycle:reactivestreams:${versions.archComponents}"
                    ]
            ],
            'rx': [
                    'java': 'io.reactivex.rxjava2:rxjava:2.1.16',
                    'android': 'io.reactivex.rxjava2:rxandroid:2.0.2',
                    'lifecycle': 'com.trello.rxlifecycle2:rxlifecycle-android:2.2.1'
            ],
            'firebase': [
                    'core': 'com.google.firebase:firebase-core:16.0.1'
            ],
            'photoView': 'com.github.chrisbanes:PhotoView:2.1.3',
            'exoPlayer': 'com.google.android.exoplayer:exoplayer:2.8.2',
            'timber': 'com.jakewharton.timber:timber:4.7.1',
            'stetho': 'com.facebook.stetho:stetho-okhttp3:1.5.0',
            'debugDb': "com.amitshekhar.android:debug-db:1.0.4",
    ]

    repositories {
        google()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.3'
        classpath 'com.google.gms:google-services:4.0.1'
        classpath deps.fabric.plugin
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url "https://jitpack.io" }
        maven { url 'https://maven.google.com/' }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
